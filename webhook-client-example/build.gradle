buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    // Use 'jcenter' for resolving your dependencies that are not in maven central yet.
    jcenter() {
      content {
        includeModule("org.gretty", "gretty-runner-jetty94:3.0.3")
        includeGroup "org.gretty"
      }
    }
  }

  dependencies {
    classpath ('gradle.plugin.org.gretty:gretty:3.0.3')
  }
}

description = "Example of how to use Webhook Client"

apply plugin: 'war'
apply plugin: 'org.gretty'

ext {
  localContextPath = '/webhook'
  localHost = 'localhost'
  localHttpPort = 8999
  localHttpsPort = 8444
  localMode = 'local'
}

war {
  archiveName = 'webhook-client-example.war'
}

// Dependencies of the project
dependencies {

  implementation rootProject

  // module dependencies
  implementation (
    'org.pageseeder.berlioz:pso-berlioz:0.11.+',
    'org.pageseeder.bastille:pso-bastille:0.10.0',
    'org.pageseeder.xmlwriter:pso-xmlwriter:1.0.+',
    'org.slf4j:slf4j-api:1.7.+',
    'javax.servlet:javax.servlet-api:3.1.0'
  )

  compileOnly (
    'org.eclipse.jdt:org.eclipse.jdt.annotation:2.0.0'
  )

  runtimeOnly(
    'ch.qos.logback:logback-core:1.1.+',
    'ch.qos.logback:logback-classic:1.1.+',
    'org.pageseeder.berlioz:pso-berlioz-kickstart:0.11.3',
  )

  testImplementation (
    'junit:junit:4.12'
  )
}

javadoc {
  failOnError= false
}

//test {
//  testLogging {events 'passed'}
//}


// Property for local Jetty dev server
gretty {
  jvmArgs = ["-Dberlioz.mode=$localMode", "-Dberlioz.appdata=local/appdata", "-Dbridge.ssl.enableUnsafe=true"]
  contextPath = localContextPath
  host = localHost
  httpPort = localHttpPort
  httpsPort = localHttpsPort
  httpsEnabled = true
  servletContainer = 'jetty9.4'
  webappCopy {
    into('WEB-INF/config') {
      from "$webAppDirName/WEB-INF/config"
      filter {
        it.replaceAll('@APP_VERSION@', version).replaceAll('@APP_DATE@', new Date().format('yyyy-MM-dd'))
      }
    }
  }
  realm 'Berlioz Admin'
  realmConfigFile "$projectDir/jetty/realm-local.properties"
  debugPort = 5005      // default
  debugSuspend = true   // default
}
